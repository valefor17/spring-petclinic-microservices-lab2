pipeline {
  agent any

  parameters {
    string(name: 'customers-service', defaultValue: '', description: 'Branch you want to deploy customers-service')
    string(name: 'vets-service', defaultValue: '', description: 'Branch you want to deploy vets-service')
    string(name: 'visits-service', defaultValue: '', description: 'Branch you want to deploy visits-service')
    string(name: 'api-gateway', defaultValue: '', description: 'Branch you want to deploy api-gateway')
    choice(name: 'developer', choices: ['thuw', 'trucs', 'quys', 'taif'], description: 'Developer name')
  }

  environment {
    HELM_REPO = 'spring-petclinic-microservices-gitops'
    GIT_REPO_URL = 'https://github.com/vantaicn/spring-petclinic-microservices-gitops.git'
    SOURCE_REPO = 'https://github.com/spring-petclinic/spring-petclinic-microservices.git'
  }

  stages {
    stage('Get Commit Hashes') {
      steps {
        script {
          // Danh sách các service và map chứa tag
          services = [
            'customers-service': 'spring-petclinic-customers-service',
            'vets-service': 'spring-petclinic-vets-service',
            'visits-service': 'spring-petclinic-visits-service',
            'api-gateway': 'spring-petclinic-api-gateway'
          ]
          tagMap = [:]

          def getCommitId = { branch ->
            return sh(script: "git ls-remote ${SOURCE_REPO} refs/heads/${branch} | cut -f1", returnStdout: true).trim().take(7)
          }

          // Duyệt từng service để lấy commit id tương ứng
          services.each { paramName, helmServiceName ->
            def branch = params[paramName]
            def tag = branch ? getCommitId(branch) : 'latest'
            tagMap[helmServiceName] = tag
            echo "${helmServiceName} -> ${tag}"
          }
        }
      }
    }

    stage('Clone Helm GitOps Repo') {
      steps {
        dir("${HELM_REPO}") {
          git url: "${GIT_REPO_URL}", credentialsId: 'github-token', branch: 'main'
        }
      }
    }

    stage('Update Helm values') {
      steps {
        script {
          def valuesFile = "${HELM_REPO}/values-${params.developer}.yaml"
          tagMap.each { helmServiceName, tag ->
            sh """
              yq e '.services["${helmServiceName}"].tag = "${tag}"' -i ${valuesFile}
            """
          }
        }
      }
    }

    stage('Commit and Push') {
      steps {
        dir("${HELM_REPO}") {
          withCredentials([usernamePassword(credentialsId: 'github-token', usernameVariable: 'GIT_USER', passwordVariable: 'GIT_PASS')]) {
            sh """
              git config user.email "ci@example.com"
              git config user.name "jenkins"
              git add values-${params.developer}.yaml
              git commit -m "Update ${params.developer} values - Build #${BUILD_NUMBER}" || echo "No changes"
              git push https://${GIT_USER}:${GIT_PASS}@github.com/vantaicn/spring-petclinic-microservices-gitops.git HEAD:main
            """
          }
        }
      }
    }

    stage('Set Build Description') {
      steps {
        script {
          def desc = services.collect { paramName, helmServiceName ->
            "${helmServiceName} = ${tagMap[helmServiceName]}"
          }.join(', ')

          currentBuild.displayName = "#${BUILD_NUMBER} - ${params.developer}"
          currentBuild.description = "${desc} <br><a href='http://${params.developer}.spring-petclinic.local'>Xem hệ thống</a>"
        }
      }
    }
  }
}
